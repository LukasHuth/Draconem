import org.gradle.internal.jvm.Jvm

plugins {
    id 'java'
    id 'application'
    id 'c'
}

group 'tk.lukashuth.draconem'
mainClassName = 'tk.lukashuth.draconem.Main'
version '1.0'

repositories {
    mavenCentral()
}

sourceCompatibility = 15
targetCompatibility = 15

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}
jar {
    manifest {
        attributes 'Main-Class': 'tk.lukashuth.draconem.Main'
    }
}
//*
application {
    applicationDefaultJvmArgs = ["-Djava.library.path=" + file("${buildDir}/libs/addon/shared").absolutePath]
}
//*/
test {
    useJUnitPlatform()
}
//*
model {
    platforms {
        x64 {
            architecture "x86_64"
        }
    }

    components {
        addon(NativeLibrarySpec) {
            targetPlatform "x64"
            binaries.all {
                def jvmHome = Jvm.current().javaHome
                if (targetPlatform.operatingSystem.macOsX) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/darwin"
                    cCompiler.args '-mmacosx-version-min=10.4'
                    linker.args '-mmacosx-version-min=10.4'
                } else if (targetPlatform.operatingSystem.linux) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/linux"
                    cCompiler.args '-o', "libaddon.lib"
                    cCompiler.args '-D_FILE_OFFSET_BITS=64'
                } else if (targetPlatform.operatingSystem.windows) {
                    cCompiler.args "-I${jvmHome}/include"
                    cCompiler.args "-I${jvmHome}/include/win32"
                    linker.args "Shlwapi.lib", "Advapi32.lib"
                } else if (targetPlatform.operatingSystem.freeBSD) {
                    cCompiler.args '-I', "${jvmHome}/include"
                    cCompiler.args '-I', "${jvmHome}/include/freebsd"
                }
            }
        }
    }
}
classes.dependsOn 'addonSharedLibrary'
//*/